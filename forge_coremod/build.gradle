plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"
    implementation project(path: ':common_loading_utils')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    dependsOn(':app:shadowJar', ':forge:build', ':common_loading_utils:build')

    from(project(':app').layout.buildDirectory.dir('libs')) {
        include 'app.jar'
        into 'META-INF/jarjar'
    }
    from(project(':forge').layout.buildDirectory.dir('libs')) {
        include 'crash_assistant-forge-' + project.version + '.jar'
        rename { String fileName ->
            "mod.jar"
        }
        into 'META-INF/jarjar'
    }
    from(project(':common_loading_utils').sourceSets.main.output)

    input.set shadowJar.archiveFile
}