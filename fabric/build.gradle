plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    implementation project(path: ':common_loading_utils')
    implementation project(path: ':common_config')

    implementation 'com.electronwill.night-config:toml:3.6.4'
    include group: 'com.electronwill.night-config', name: 'toml', version: '3.6.4'
    include group: 'com.electronwill.night-config', name: 'core', version: '3.6.4'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    dependsOn(':app:shadowJar', ':common_loading_utils:build')

    from(project(':app').tasks.shadowJar) {
        into 'META-INF/jarjar'
    }
    from(project(':common_loading_utils').sourceSets.main.output)
    from(project(':common_config').sourceSets.main.output)

    input.set shadowJar.archiveFile
}